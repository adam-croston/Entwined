plugins {
	id 'application'
	id 'java'
	id "com.diffplug.spotless" version "5.8.1"
}

def libPath = "${projectDir}/LXStudio-IDE/lib/"
def lxVersion = "0.2.1"

repositories {
	mavenCentral()
}

dependencies {
	implementation files("${libPath}/lx-${lxVersion}-jar-with-dependencies.jar")
	implementation files("${libPath}/p3lx-${lxVersion}.jar")
	implementation files("${libPath}/lxstudio-${lxVersion}.jar")
	implementation fileTree("${libPath}/processing-3.5.4")
	implementation files("${projectDir}/oldlx/Trees/code/toxiclibscore.jar")

	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
	implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'

	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.2'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.0'

	// https://mvnrepository.com/artifact/com.google.guava/guava
	compile group: 'com.google.guava', name: 'guava', version: '29.0-jre'

}

mainClassName = 'com.charlesgadeken.entwined.EntwinedGui'

jar {
	manifest {
		attributes["Main-Class"] = "com.charlesgadeken.entwined.EntwinedGui"
		attributes["Implementation-Title"] = "entwined"
		attributes["Implementation-Version"] = "0.0.1"
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

spotless {
	groovyGradle {
		target '*.gradle' // default target of groovyGradle
		greclipse()
	}
	java {
		googleJavaFormat('1.7').aosp()
	}
}

tasks.withType(Test).configureEach {
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}
